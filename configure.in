# Process this file with autoconf to produce a configure script.

# Initialize
AC_PREREQ([2.53])
AC_INIT([gnome-hearts], [0.3], [http://bugzilla.jejik.com/])
AM_INIT_AUTOMAKE
AC_CONFIG_SRCDIR([src/hearts.c])
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

# Scrollkeeper for the documentation
SCROLLKEEPER_BUILD_REQUIRED=0.3.5
AC_SUBST(SCROLLKEEPER_BUILD_REQUIRED)

# Find a C compiler and some i18n tools
AC_PROG_CC
AC_LANG_C
IT_PROG_INTLTOOL([0.29])

# A little hack for intltool to make po/LINGUAS work with version 0.34 and below
if test $INTLTOOL_APPLIED_VERSION_AS_INT -le "34"; then
  ALL_LINGUAS="`grep -v '^#' "$srcdir/po/LINGUAS"`"
fi

# Check for libraries. gmodule is required to make sure --export-dynamic is used and glade works
PKG_CHECK_MODULES(GNOME_HEARTS, [libgnomeui-2.0 gtk+-2.0 libglade-2.0 glib-2.0 gmodule-2.0])
AC_SUBST(GNOME_HEARTS_CFLAGS)
AC_SUBST(GNOME_HEARTS_LIBS)

# Check for Python
AM_PATH_PYTHON(2.4)

# Find Python compiler and linker flags
PYTHON_LIBS=[$($PYTHON -c "from distutils import sysconfig
print '/'.join(sysconfig.get_config_vars('LIBPL','LDLIBRARY'))+' '+sysconfig.get_config_var('LIBS')+' '+sysconfig.get_config_var('LINKFORSHARED')")]
PYTHON_CFLAGS=[$($PYTHON -c "from distutils import sysconfig
print sysconfig.get_python_inc()")]
PYTHON_CFLAGS="-I$PYTHON_CFLAGS"
if test "$PYTHON_LIBS" = ""; then
  AC_MSG_RESULT(not found)
else
  ENABLED_MODULES="$ENABLED_MODULES -DENABLE_PYTHON_MODULES"
fi

# Find the Python headers
AM_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR(could not find Python headers)])

# expand the python directory
if test "x${prefix}" = "xNONE"; then
	PYTHON_DIR=$ac_default_prefix"/share"
else
	PYTHON_DIR=$prefix"/share"
fi

# Set python variables
AC_SUBST(PYTHON_CFLAGS)
AC_SUBST(PYTHON_LIBS)
AC_SUBST(PYTHON_DIR)
AC_DEFINE_UNQUOTED(PYTHON_DIR, "$PYTHON_DIR", [Python directory])


# Check for scrollkeeper
AC_PATH_PROG(SCROLLKEEPER_CONFIG, scrollkeeper-config,no)
if test x$SCROLLKEEPER_CONFIG = xno; then
  AC_MSG_ERROR(Couldn't find scrollkeeper-config. Please install the scrollkeeper package: http://scrollkeeper.sourceforge.net)
fi

# Set up gettext support
GETTEXT_PACKAGE=gnome-hearts
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])
AM_GLIB_GNU_GETTEXT

#define some directories
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/share/locale", [Package local directory])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/share/locale", [Package local directory])
fi

if test "x${prefix}" = "xNONE"; then
   AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share", [Data directory])
else
   AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share", [Data directory])
fi

if test "x${prefix}" = "xNONE"; then
   CONFIG_DATA_DIR=${ac_default_prefix}/share
else
   CONFIG_DATA_DIR=${prefix}/share
fi
AC_SUBST(CONFIG_DATA_DIR)

#output all required files
AC_OUTPUT([
Makefile
help/Makefile
help/C/Makefile
help/de/Makefile
help/en_GB/Makefile
help/es/Makefile
help/it/Makefile
help/nl/Makefile
help/pl/Makefile
help/pt/Makefile
help/tl/Makefile
help/zh_CN/Makefile
man/Makefile
pixmaps/Makefile
po/Makefile.in
scripts/Makefile
scripts/hearts.py
scripts/players/Makefile
src/Makefile
src/gnome-hearts.cfg
])
